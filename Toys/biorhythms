ZXASCII
AUTO -1
PROG demos:toys/biorhythms
CHANGED FALSE
10 PAPER 8: INK 0: CLS
20 def proc g(d,m,y):
    m=(m+9) mod 12,
    y=y-m div 10,
    result=365*y+y div 4-y div 100+y div 400+(m*306+5) div 10+(d-1):
 end proc:
 def fn y(i,j)=sh+sin((tau/j)*i)*yo
30 input scale 2,2;at 1,1;format "##/##/####";"Date of birth? ";a$:
 dob=val a$( to 2),
 mob=val a$(4 to 5),
 yob=val a$(7 to 10),
 t=time,
 curd=day t,
 curm=month t,
 cury=year t,
 t=call g(curd,curm,cury)-call g(dob,mob,yob),
 xsc=scrw/60,sh=scrh/2,yo=sh-120,so=t-30,n=0
40 cls:
 for y=0 to scrh step xsc:
    plot 0,y:
    draw ink 235;scrw,0:
 next y:
 for x=-30 to 30:
    x1=(t+x-so)*xsc:
    plot x1,0:Draw ink 235;0,scrh:
    if x=0 then rectangle x1-1,0 to x1+1,scrh fill: end if:
 next x
50 for d=so to t+30 step 1/(xsc*2):
    x=(d-so)*xsc:
    circle ink 5;x,fn y(d,23),2 fill:
    circle ink 6;x,fn y(d,28),2 fill:
    circle ink 4;x,fn y(d,33),2 fill:
    n+=1: if not n mod 8 then
       wait screen:
55    print paper 2;scale 2,2;at 1,1;" "*14;at 2,1;ink 13;" PHYSICAL     ";AT 3,1;INK 14;" EMOTIONAL    ";AT 4,1;INK 12;" INTELLECTUAL ";at 5,1;" "*14
60    l=len a$+2,o=txtw-1-l:
    print scale 2,2;paper 2;ink 15;at 1,o;" "*l;at 2,o;" "+a$+" ";at 3,o;" "*l:
 next d
70 pause 0: run
