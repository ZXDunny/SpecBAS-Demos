ZXASCII
AUTO -1
PROG demos:graphics/digitalrain
CHANGED FALSE
10 PAPER 0: CLS: screen lock: sc=1: FPS 60/sc:
 palette 1,$ff,$ff,$fF:
 FOR f=1 to 64: palette 70-f,0,(f/64)*255,0: next f: rainbow 1 to 6:
 scale sc,sc
20 def fn c$=CHR$((rnd*94)+33)
30 nr=200/sc:
 dim r(nr,4);r$(nr):
 for f=1 to nr: go sub 80: next f
40 for f=1 to nr:
    y=r(f,2),x=r(f,1),l=r(f,3):
    if y>=0 then
       text at y,x;ink 1;r$(f)(y+1):
       if y>1 then
          clr=2,h=y-1:
          for g=h to max(y-l,-1) step -1:
             text at g,x;ink clr;r$(f)(max(1,h+1)):
             h-=1,clr+=64/l:
          next g:
          do 3: r$(f)(1+(rnd*len r$(f)))=fn c$: loop
50    r(f,2)+=r(f,4):
    if y>l+txth then
       go sub 80
60 next f:
 wait screen:
 cls
70 go to 40
80 r(f,1)=int(rnd*txtw)
90 r(f,2)=-rnd*txth,
 r(f,3)=int(rnd*(txth)/3)+15,
 r(f,4)=(.25+sqr rnd)/2.5,
 r$(f)="":
 for g=1 to txth*2:
    r$(f)=r$(f)+fn c$:
 next g:
 return
