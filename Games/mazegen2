ZXASCII
AUTO -1
PROG mazegen2
10 REM Maze Generator
20 LET mw=99: LET mh=59: GO SUB 2000
30 LET m$="ð"*mw*mh: LET b$="1"*mw+(("1"+(" "*(mw-2))+"1")*(mh-2))+("1"*mw): LET cc=mw+2: DIM of=-1,-mw,1,mw: LET m$(cc)=" ": LET stk$=QCHR$ cc: DIM nb(4)
40 LET nn=0: FOR n=1 TO 4: IF b$(cc+of(n))<>"1" THEN IF m$(cc+(of(n)*2))<>" " THEN INC nn: LET nb(nn)=n
50 NEXT n
60 IF nn>0 THEN LET o=of(nb(INT(RND*nn)+1)): LET m$(cc+o)=" ": LET m$(cc+(o*2))=" ": GO SUB 1000: LET stk$=(QCHR$(cc) AND nn>1)+stk$: INC cc,o*2: GO TO 40
70 IF stk$<>"" THEN LET cc=QCODE stk$: LET stk$=stk$(5 TO): GO TO 40
80 PAUSE 0: RUN 
1000 LET c=cc+o: PRINT AT c DIV mw,c MOD mw;" ": LET c=cc+o*2: PRINT AT c DIV mw,c MOD mw;" ": RETURN 
2000 FOR x=1 TO mw: FOR y=0 TO mh-1: PRINT AT y,x;"ð": NEXT y: NEXT x: PRINT AT 1,2;" ": RETURN 
