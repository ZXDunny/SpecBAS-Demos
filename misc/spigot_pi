ZXASCII
AUTO -1
PROG /demos/misc/spigot_pi
CHANGED FALSE
100 REM Spigot PI by Srba
110 INPUT "Number of digits = ";n: LET n=INT n: IF n<=0 THEN GO TO 110:
120 LET b=INT ((10*n+2)/3): LET h=0: DIM m(b): FOR i=1 TO b: LET m(i)=2: NEXT i:
130 LET p$="":
140 FOR i=1 TO n: LET s=0: LET c=0:
150 FOR j=b-1 TO 0 STEP -1: LET m=j+1: LET k=2*j+1
160 LET s=10*m(m)+c: LET v=INT (s/k): LET m(m)=s-v*k: LET c=v*j
170 NEXT j: LET m(1)=s-10*INT (s/10):
180 LET d=INT (s/10):
190 IF d=9 THEN LET h=h+1: GO TO 220
200 IF d=10 THEN LET d=0: FOR j=1 TO h: LET k=i-j: LET v=VAL p$(k)+1: LET p$(k)=STR$ (v-10*INT (v/10)): NEXT j
210 LET h=1:
220 LET p$=p$+STR$ d
230 PRINT AT 0,0;"PI(";i;") = ";p$(1)+"."+p$(2 TO ):
240 NEXT i:
250 LET p$=p$(1)+"."+p$(2 TO )
