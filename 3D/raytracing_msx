ZXASCII
AUTO -1
PROG demos:3d/raytracing_msx
CHANGED FALSE
10 rem Screen init
20 window depth 0,32: CLS: option "base",0
30 rem Initialize
40 DIM T(10),V(16),O(20,8),S(20,8),RI(4),GI(4),BI(4),I(4),Z(4),Y(4)
50 FOR I=0 TO 9:READ T(I):NEXT I
60 V(0)=T(0):V(1)=T(1):V(2)=T(2)
70 V(9)=T(0)-T(3):V(10)=T(1)-T(4):V(11)=T(2)-T(5)
80 V=SQR(V(9)*V(9)+V(10)*V(10)+V(11)*V(11))
90 V(9)=V(9)/V:V(10)=V(10)/V:V(11)=V(11)/V
100 V(6)=-V(9)*V(10):V(7)=1-V(10)*V(10):V(8)=-V(11)*V(10)
110 V(3)=-(V(10)*V(8)-V(11)*V(7)):V(4)=-(V(11)*V(6)-V(9)*V(8))
120 V(5)=-(V(9)*V(7)-V(10)*V(6)):V(15)=T(9)
130 V(12)=T(6):V(13)=T(7):V(14)=T(8)
140 READ MO:FOR I=0 TO MO-1:FOR J=0 TO 7:READ O(I,J):NEXT J;I
150 READ MS:FOR I=0 TO MS-1:FOR J=0 TO 7:READ S(I,J):NEXT J;I
160 MA=1000:MI=1E-01:MD=0:PT=4:SC=SCRH/4.75:SCW=SCRW/2,SCH=SCRH/2
170 FOR I=1 TO 4
180 V=SQR(V(I*3+0)*V(I*3+0)+V(I*3+1)*V(I*3+1)+V(I*3+2)*V(I*3+2))
190 V(I*3+0)=V(I*3+0)/V:V(I*3+1)=V(I*3+1)/V:V(I*3+2)=V(I*3+2)/V
200 NEXT I
210 rem Trace
220 FOR SY=0 TO scrh-1:FOR SX=0 TO scrw-1:XD=0:YD=0
230 CX=V(0):CY=V(1):CZ=V(2)
240 VX=V(3)*(SX-SCW)/SC+V(6)*(SCH-SY)/SC-V(9)*V(15)
250 VY=V(4)*(SX-SCW)/SC+V(7)*(SCH-SY)/SC-V(10)*V(15)
260 VZ=V(5)*(SX-SCW)/SC+V(8)*(SCH-SY)/SC-V(11)*V(15)
270 V=SQR(VX*VX+VY*VY+VZ*VZ)
280 VX=VX/V:VY=VY/V:VZ=VZ/V
290 CR=0:CG=0:CB=0:RN=0:RF=1
300 GO SUB 380
310 IF CR>=1 THEN CR=.99
320 IF CG>=1 THEN CG=.99
330 IF CB>=1 THEN CB=.99
340 CC=INT(CR*256)*65536+INT(CG*256)*256+INT(CB*256)
350 PLOT ink CC;SX,SY
360 NEXT SX;SY
370 stop
380 rem Pixel
390 TT=MA
400 FOR N=0 TO MO-1
410 GO SUB 490
420 IF TT>T AND T>MI THEN TT=T:TN=N:LX=NX:LY=NY:LZ=NZ
430 NEXT N
440 IF TT=MA THEN GO to 480
450 CX=CX+TT*VX:CY=CY+TT*VY:CZ=CZ+TT*VZ:N=TN
460 GO SUB 770
470 IF F=1 THEN GO TO 380
480 RETURN
490 rem Cross
500 RX=CX-O(N,0):RY=CY-O(N,1):RZ=CZ-O(N,2)
510 A=O(N,3):B=O(N,4):C=O(N,5)
520 IF O(N,6) THEN GO to 650
540 rem Box
550 IF VX=0 THEN T1=MA ELSE IF RX<0 THEN T1=-(RX+A)/VX ELSE T1=-(RX-A)/VX
560 IF VY=0 THEN T2=MA ELSE IF RY<0 THEN T2=-(RY+B)/VY ELSE T2=-(RY-B)/VY
570 IF VZ=0 THEN T3=MA ELSE IF RZ<0 THEN T3=-(RZ+C)/VZ ELSE T3=-(RZ-C)/VZ
580 IF ABS(RY+T1*VY)>B OR ABS(RZ+T1*VZ)>C THEN T1=MA
590 IF ABS(RZ+T2*VZ)>C OR ABS(RX+T2*VX)>A THEN T2=MA
600 IF ABS(RX+T3*VX)>A OR ABS(RY+T3*VY)>B THEN T3=MA
610 IF T1<=T2 AND T1<=T3 THEN T=T1:NX=-VX/ABS(VX):NY=0:NZ=0
620 IF T2<=T3 AND T2<=T1 THEN T=T2:NY=-VY/ABS(VY):NZ=0:NX=0
630 IF T3<=T1 AND T3<=T2 THEN T=T3:NZ=-VZ/ABS(VZ):NX=0:NY=0
640 RETURN
650 rem Ball
660 AA=VX*VX*A+VY*VY*B+VZ*VZ*C
670 BB=RX*VX*A+RY*VY*B+RZ*VZ*C
680 CC=RX*RX*A+RY*RY*B+RZ*RZ*C-1
690 DD=BB*BB-AA*CC
700 IF DD<0 THEN T=MA:GO TO 760
710 T1=(-BB-SQR(DD))/AA:T2=(-BB+SQR(DD))/AA
720 IF T1<T2 THEN T=T1 ELSE T=T2
730 NX=A*(RX+T*VX):NY=B*(RY+T*VY):NZ=C*(RZ+T*VZ)
740 M=SQR(NX*NX+NY*NY+NZ*NZ)
750 NX=NX/M:NY=NY/M:NZ=NZ/M
760 RETURN
770 rem Shade
780 SH=O(N,7)
790 IF SH<>-1 THEN GO to 840
800 PX=INT(ABS(CX+100)/PT-(CX+100<0))
810 PY=INT(ABS(CY+100)/PT-(CY+100<0))
820 PZ=INT(ABS(CZ+100)/PT-(CZ+100<0))
830 SH=(PX+PY+PZ) MOD 2
840 SR=S(SH,0):SG=S(SH,1):SB=S(SH,2)
850 SA=S(SH,3):SD=S(SH,4):SF=S(SH,5)
860 SP=S(SH,6):SE=S(SH,7)
870 JX=V(12)-VX:JY=V(13)-VY:JZ=V(14)-VZ
880 JN=SQR(JX*JX+JY*JY+JZ*JZ)
890 SM=(LX*JX+LY*JY+LZ*JZ)/JN
900 IF SM<0 THEN SM=0
910 FOR P=1 TO SE:SM=SM*SM:NEXT P
920 VN=-2*(LX*VX+LY*VY+LZ*VZ)
930 WX=VX+VN*LX:WY=VY+VN*LY:WZ=VZ+VN*LZ
940 VX=V(12):VY=V(13):VZ=V(14)
950 SN=LX*VX+LY*VY+LZ*VZ
960 IF SN<0 THEN SN=0
970 FOR N=0 TO MO-1
980 GO SUB 490
990 IF MA>T AND T>MI THEN SN=0:SM=0
1000 NEXT N
1010 CR=CR+(SR*(SA+SD*SN)+SP*SM)*RF
1020 CG=CG+(SG*(SA+SD*SN)+SP*SM)*RF
1030 CB=CB+(SB*(SA+SD*SN)+SP*SM)*RF
1040 IF SF=0 AND RN<4 THEN F=0:GO TO 1070
1050 F=1:RF=RF*SF:RN=RN+1
1060 VX=WX:VY=WY:VZ=WZ
1070 RETURN
1080 rem Picture data
1090 DATA 40, 20, 20: REM Camera rotation
1100 DATA 5, -5, 0:   REM Camera position
1110 DATA -8, 9, -3:  REM Light source position
1120 DATA 6:          rem Camera zoom
	
1130 DATA 6:          REM Number of objects
	
1140 DATA 16, 2, 10:  rem x,y,z position
1150 DATA .2, .2, .2: rem x,y,z size (greater = smaller)
1160 DATA 1, 2:       rem Type (1=sphere,2=box),colour index
	
1170 DATA 6, 2, 10
1180 DATA .2, .2, .2
1190 DATA 1, 5
	
1200 DATA -4, 2, 10
1210 DATA .2, .2, .2
1220 DATA 1, 4
	
1230 DATA 14, 3, -8
1240 DATA .1, .1, .1
1250 DATA 1, 6
	
1260 DATA 4, 6, 0
1270 DATA .025, .025, .025
1280 DATA 1, 6
	
1290 DATA 0, -2, 0
1300 DATA 20, .5, 20
1310 DATA 0, -1
	
1320 DATA 7:          REM Number of colorurs
	
1330 DATA .9, .9, .3: REM RGB values (0..1) - grid yellow
1340 DATA .5, .4, 0:  rEM brightness, specular, reflectivity
1350 DATA .7, 6:      REM
	
1360 DATA 0, .5, 0:   rem grid green
1370 DATA .5, .4, 0
1380 DATA .7, 6
	
1390 DATA .9, .0, .0: rem red ball
1400 DATA .3, .6, .2
1410 DATA 0, 0
	
1420 DATA .9, .9, .9: rem grey ball
1430 DATA .3, .6, 0
1440 DATA .6, 8
	
1450 DATA .0, .0, .9: rem blue ball
1460 DATA .3, .6, .2
1470 DATA .6, 8
	
1480 DATA 0, .9, 0:   rem green ball
1490 DATA .3, .6, .2
1500 DATA 1, 6
	
1510 DATA 0, 0, 0:    rem chrome ball
1520 DATA .3, .6, .7
1530 DATA .9, 1
