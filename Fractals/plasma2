ZXASCII
AUTO -1
PROG demos:fractals/plasma2
CHANGED FALSE
10 REM Plasma II
20 DEF FN dist(a,b,c,d)=sqr(((a-c)*(a-c))+((b-d)*(b-d)))
30 FOR f=0 TO 255: 
	   PALETTE f,255-f,ABS(f-128),f: 
	NEXT f
40 d=4,w=scrw,dt=PI,h=scrh,
	k0=w/16,k1=w/2,k2=h/2,k3=w/4,k4=h/4,k5=(w/4)*3,k6=k4,k7=k5,k8=w*(64/25): 
	OPTION "base",0
50 DIM mem(w,h,3)
60 FOR x=0 TO w-1 STEP d: 
	   FOR y=0 TO h-1 STEP d: 
				   mem(x,y,0)=COS(FN dist(x,y,k3,k4)/((w/k0)+4)),
       mem(x,y,1)=SIN(FN dist(x,y/((w/k0)-1),k5,k6)/((w/k0)-3)), 
							mem(x,y,2)=COS(FN dist(x,y,k7,k8)/(w/k0)): 
				NEXT y: 
	NEXT x
70 t=0
80 t+=dt:
	FOR x=0 TO w-d STEP d;y=0 TO h-d STEP d:   
	   RECTANGLE INK 128+32*(SIN(FN dist(x+t,y-t,k1,k2)/8)+mem(x,y,0)+mem(x,y,1)+mem(x,y,2));x,y TO x+d,y+d FILL: 
	NEXT y;x: 
	GO TO 80
