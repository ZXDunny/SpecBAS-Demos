ZXASCII
AUTO -1
PROG demos:toys/boingball
CHANGED FALSE
10 REM Amiga Boing Ball
20 PALETTE 0,0;16,0;252,$808080;253,$606060;254,$A000A0;255,$600060: RAINBOW 0 TO 16:
 screen lock
30 rad=109,r2=rad*2,rad2=rad^2,scl=1024,nc=128,nc2=nc/2,
 norm=TAU/scl,ang=DEGTORAD 18,k=4,amb=1
40 CLS 0:DIM p(2,2);l=-0.348,0.348,-0.87 BASE 0:
 window new w,0,0,rad*2,rad*2 bpp 32 alpha: cls $0:
 for y=-rad to rad step .75:
    for x=-rad to rad step .75:
       if sqr(x*x+y*y)<rad then
          tmp=x/sqr(rad2-y*y),xres=atn(tmp/sqr(1-tmp*tmp))/norm,
          tmp=y/rad,yres=atn(tmp/sqr(1-tmp*tmp))/norm,
          col=xres,col=iif(yres mod nc<nc2,col+nc2,col),
          v0=y,v1=x,v2=SQR(rad*rad-y*y-x*x),
          l=SQR(v0*v0+v1*v1+v2*v2),v0,v1,v2/=l,p(1,1)=x,p(1,2)=y,
          i=MIN(((MIN(0,l(0)*v0+l(1)*v1+l(2)*v2)^k)+amb)*255,255)/3 shl 24:
          transform2d p() rotate ang: x1=p(1,1)+rad,y1=p(1,2)+rad:
          window w: PLOT INK i;x1,y1:
          window 0: plot ink 1+(col mod nc);x1,y1:
       end if:
    next x:
 next y:
 window 0:
 window grab b$,0,0,0,r2,r2 trans 0
50 cls:
 for f=1 to nc: palette f,iif(int(f/nc2) mod 2,$FFFFFF,$FF0000): next f:
 for f=nc-1 to nc-8 step -1: palette f,$FFCCBB: next f
60 PAPER 252: CLS: ss=scrw/24,sc=int(scrw/ss),sm=scrh/ss: ink 254
70 for y=1 to sm-1: rectangle ss,y*ss,ss*(sc-2),2 fill: next y:
 for x=ss to ss*sc step ss: RECTANGLE x,ss,2,ss*12+2 fill: next x:
 window grab s$,0,0,0,scrw,scrh:
 SAMPLE NEW boing,"boing.wav"
80 x=scrw/2-rad,y=20,xd=3,yd=0,ya=0.05,a=0
90 DO:
    window put s$,0,0,0:
    circle ink 1;over 2;x+rad/2.5+rad,(y*0.8)+rad,rad*1.05 fill:
    window put b$,0,x,y:
    window move w,x,y
100    if (x+xd>scrw-r2) or (x<0) then
       SAMPLE PLAY boing RATE 4800 pan sgn xd:
       xd=-sgn xd*((RND*3)+2): x=iif(x<0,0,scrw-r2)
110    if y+yd>scrh-r2 then
       yd=-((rnd*4)+2): y=scrh-r2:
       SAMPLE PLAY boing RATE 3000 pan map(x+rad,rad to scrw-rad,-1 to 1)
120    ox=int x,x+=xd,y+=yd,yd+=ya,ox=abs(ox-int x)*2,a+=ox:
    do while a>=1:
       a-=1: palette shr sgn xd,1 to nc:
    loop
130    wait screen:
 loop
