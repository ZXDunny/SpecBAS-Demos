ZXASCII
AUTO -1
PROG /demos/3d/spinny2
CHANGED FALSE
10 REM Spinny Cube by Dean Belfield
20 option "base",0: j=255,s=220,scl=scrw/800:
 for f=s to 253: palette hsv f,((f-s)*16) mod 360,255,255-max(0,j-=6.25): next f:
 palette 0,0;128,0;254,0;255,$FFFFFF: rainbow 0 to 128;128 to s:
 paper 0: ink 255: cls: screen lock:
 dim a(8);b(8):
 xd,yd,xo,yo=0,sd=1024*scl,od=256*scl,sw=scrw/2,sh=scrh/2,a=25*scl,bw=352*scl,bw2=bw/2:
 dim sp=((-a,a,a),(a,a,a),(-a,-a,a),(a,-a,a),(-a,a,-a),(a,a,-a),(-a,-a,-a),(a,-a,-a)):
 dim sh=((0,1,3,2),(6,7,5,4),(1,5,7,3),(2,6,4,0),(2,3,7,6),(0,4,5,1))
30 the,psi,phi=0:
 do:
    GO SUb 40:
    the+=.01,psi+=.03,phi-=.02:
    wait screen:
    rectangle ink 2;over 13;sw-bw2,sh-bw2,bw,bw fill:
 loop
40 cphi=cos phi,sphi=sin phi,cthe=cos the,sthe=sin the,
 cpsi=cos psi,spsi=sin psi:
 For i=0 to 7:
    xx=sp(i,0),yy=sp(i,1),zz=sp(i,2),
    y=yy*cphi-zz*sphi,zz=yy*sphi+zz*cphi,
    x=xx*cthe-zz*sthe,zz=xx*sthe+zz*cthe,
    xx=(x*cpsi-y*spsi)+xo+xd,yy=(x*spsi+y*cpsi)+yo+yd,
    a(i)=sw+xx*sd/(od-zz),
    b(i)=sh+yy*sd/(od-zz):
 next i:
 for i=0 to 5:
    x1=a(sh(i,0)),x2=a(sh(i,1)),x3=a(sh(i,2)),x4=a(sh(i,3)),
    y1=b(sh(i,0)),y2=b(sh(i,1)),y3=b(sh(i,2)),y4=b(sh(i,3)):
    if x1*(y2-y3)+x2*(y3-y1)+x3*(y1-y2)<=0 then
       polygon x1, y1 to x2, y2 to x3,y3 to x4,y4 fill:
       polygon ink 254;x1, y1 to x2, y2 to x3,y3 to x4,y4
50 next i:
 return
