ZXASCII
AUTO -1
PROG demos:3d/globe_lines
CHANGED FALSE
10 u=scrw/2,v=scrh/2,r=scrh*0.4,d=TAU/24,e=.1744,a,b,c=0
20 a+=.02,b+=.017,c+=.025,
 s1=SIN a,s2=SIN b,s3=SIN c,
 c1=COS a,c2=COS b,c3=COS c,
 ax=c2*c3,ay=-c2*s3,az=s2,
 bx=c1*s3+s1*s2*c3,by=c1*c3-s1*s2*s3,bz=-s1*c2,
 cx=s1*s3-c1*s2*c3,cy=s1*c3+c1*s2*s3,cz=c1*c2
30 CIRCLE INK 0;u,v,r FILL: INK 8: SCREEN LOCK
40 FOR l=0 TO pi-d STEP d:
    f1=0:
    FOR p=0 TO tau STEP e:
       GO SUB 90:
       IF yy>0 THEN
          f2,f1=0:
       else
          xb=u+xx,yb=v+zz,f2=1:
          IF f1=0 THEN
             x1=xb,y1=yb,f1=1
          else
             DRAW x1,y1 to xb,yb:
             x1=xb,y1=yb,f1=f2
50    NEXT p:
 NEXT l

60 FOR p=-pi/2 TO pi/2 STEP d:
    f1=0:
    FOR l=0 TO tau STEP e:
       GO SUB 90:
       IF yy>0 THEN
          f1,f2=0
       else
          xb=u+xx,yb=v+zz,f2=1:
          IF f1=0 THEN
             x1=xb,y1=yb,f1=1
          else
             DRAW x1,y1 to xb,yb:
             x1=xb,y1=yb,f1=f2
70    NEXT l:
 NEXT p
80 WAIT SCREEN: GO To 20
90 x=r*COS p*COS l,y=r*COS p*SIN l,z=r*SIN p,
 xx=ax*x+ay*y+az*z,yy=bx*x+by*y+bz*z,zz=cx*x+cy*y+cz*z:
 RETURN
