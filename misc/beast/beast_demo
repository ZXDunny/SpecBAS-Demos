ZXASCII
AUTO -1
PROG demos:misc/beast/beast_demo
CHANGED FALSE
10 sample new mus,"music.mp3":
 WINDOW NEW moon,220,16,0,0 TRANS 0 load "moon.png"
20 window new b1,0,20,640,27 trans 0 load "blimp_big.png"
30 WINDOW NEW b2,0,48,640,13 TRANS 0 load "blimp_small.png"
40 restore: nw=14: DIM w(nw): FOR f=1 TO nw: READ a$,y,t: window new w(f),0,y,0,0 trans t load a$: next f
50 data "cloud1.png",0,0,"cloud2.png",23,0,"cloud3.png",63,0,"cloud4.png",82,0,"cloud5.png",91,0,"mountains.png",80,6,"grass1.png",152,255,"grass2.png",154,255,"grass3.png",157,255,"grass4.png",164,255,"grass5.png",171,255,"trees2.png",-20,0,"fence.png",156,0,"trees.png",-20,20
60 rem sprites
70 window new sp,320,120,32,52 trans 0: dim sp(6): for f=1 to 6: graphic new sp(f) load "dude"+str$ f+".png": next f: palette copy sp(1),0,256 to 0: for f=1 to 6: graphic remap sp(f): next f
80 window new bat,-640,60,32,64 trans 0: dim bat(5): for f=1 to 5: graphic new bat(f) load "bat"+str$ f+".png": next f: palette copy bat(1),0,256 to 0: for f=1 to 5: graphic remap bat(f): next f: bat$=#1#2#3#4#5#4#3#2
90 window mirror w(12);roll w(12),2048,0
100 c=0,dudeflag=320,dudeframe=1,batx=-640,baty=60
110 rem create gradient
120 window 0: degrees: paper 0: mouse hide: screen lock:
 WINDOW SIZE 0,320,180: WINDOW DEPTH 0,32:
 cls rgbtoint(102,119,136):
 r=102,g=119,b=136:
 for y=69 to 180:
    draw ink rgbtoint(r,g,b);0,y to scrw,y: r+=1.37:
 next y
130 rem Do the demo
140 sample play mus loop
150 window roll w(1),1,0: if c&3>0 then ww=2 else ww=1
160 window roll w(12),ww,0
170 window roll w(14),2,0
180 if c&1 then window roll w(2),1,0: window roll w(6),1,0
190 FOR f=3 TO 5: IF NOT (c MOD f) THEN WINDOW ROLL w(f),1,0
200 next f:for f=7 to 11: window roll w(f),(f-6)+1,0
210 NEXT f
220 window roll w(13),6,0
230 if not (c mod 3) then window roll b1,1,0
240 IF NOT (c MOD 7) THEN WINDOW ROLL b2,-1,0
250 if dudeflag>144 then dudeflag-=.25:window move sp,dudeflag,105
260 if not (c mod 6) then window put graphic sp(dudeframe),sp,0,0:inc dudeframe,1,1 to 6
270 if not (c mod 6) then window put graphic bat(code bat$(1)),bat,0,0:bat$=bat$(2 to)+bat$(1)
280 if batx<scrw+100 then window move bat,batx,baty:batx+=3 else batx=-(640+(320*rnd*12)):baty=rnd*80
290 c+=1:WAIT SCREEN: GO To 150
