ZXASCII
AUTO -1
PROG demos:fractals/clouds
CHANGED FALSE
10 REM Plasma clouds
20 LET r=RND*3: LET bands=2^INT((RND*4)+3): DIM clr(bands+1,3): FOR f=1 TO bands: LET clr(f,1)=RND*256: LET clr(f,2)=RND*256: LET clr(f,3)=RND*256: NEXT f: LET clr(bands+1,1)=clr(1,1): LET clr(bands+1,2)=clr(1,2): LET clr(bands+1,3)=clr(1,3): LET bsz=256/bands: LET i=0: FOR f=1 TO bands: LET rs=(clr(f+1,1)-clr(f,1))/bsz: LET gs=(clr(f+1,2)-clr(f,2))/bsz: LET bs=(clr(f+1,3)-clr(f,3))/bsz: FOR g=1 TO bsz: PALETTE i,clr(f,1)+(g*rs),clr(f,2)+(g*gs),clr(f,3)+(g*bs): INC i: NEXT g: NEXT f: PALETTE 0,0,0,0
30 PAPER 0: CLS: LET w=512: LET h=384
40 PLOT INK 1+RND*255;0,0;INK 1+RND*255;w,0;INK 1+RND*255;0,h;INK 1+RND*255;w,h
50 PROC divide(0,0,w,h)
60 STOP
70 DEF PROC divide(x1,y1,x2,y2)
80 LOCAL nx=int(x1+(x2-x1)/2),ny=int(y1+(y2-y1)/2)
90 IF INT nx>x1 AND INT ny>y1 THEN LET r=(nx-x1)*2: LET tl=POINT(x1,y1): LET tr=POINT(x2,y1): LET bl=POINT(x1,y2): LET br=POINT(x2,y2): PLOT INK (tl+tr)/2;nx,y1;INK (tl+bl)/2;x1,ny;INK (bl+br)/2;nx,y2;INK (tr+br)/2;x2,ny;INK MIN(MAX(((tl+tr+bl+br)/4)+(RND*r)-(r/2),1),255);nx,ny: PROC divide(x1,y1,nx,ny): PROC divide(nx,y1,x2,ny): PROC divide(x1,ny,nx,y2): PROC divide(nx,ny,x2,y2)
100 END PROC
