ZXASCII
AUTO -1
PROG /demos/3D/lightsource_ball
CHANGED FALSE
10 rem Sphere shader. Slow as.
     lx,ly,lz=light source coords
20 palette 255,$FFFFFF: rainbow 0 to 255: screen lock:
 for f=0 to 400: plot ink rnd*255;rnd*scrw,rnd*scrh: next f
30 llx,lly=0,xc=scrw/3,yc=scrh/2,r=75,amb=0.03,k=2,n=24,mxp=(1-amb)*256,r2=r*r:
 dim no(r*2+1,r*2+1) base 0:
 for x=-r to r:
    for y=-r to r:
       no(x+r,y+r)=rnd*n:
    next y:
 next x
40 lx=xc-mousex,ly=yc-mousey,lz=-75:
 if lx<>llx or ly<>lly then
    GO SUB 1000:
    llx=lx,lly=ly
50 wait screen:
 GO To 40
1000 l=sqr(lx*lx+ly*ly+lz*lz),
 nlx=lx/l,nly=ly/l,nlz=lz/l:
 for x=-r to r:
    x2=x*x:
    for y=-r to r:
       y2=y*y:
       if x2+y2<=r2 then
          v2=sqr(r2-x2-y2),
          l=sqr(x2+y2+v2*v2),
          v0=x/l,v1=y/l,v2/=l,
          d=nlx*v0+nly*v1+nlz*v2,
          i=mxp*(iif(d<0,-d^k,0)+amb)-no(x+r,y+r):
          plot ink clamp(i,0 to 255);x+xc,y+yc
1010    next y:
 next x:
 return
