ZXASCII
AUTO -1
PROG demos:graphics/bernoulli
CHANGED FALSE
10 INK 0: PAPER 8: CLS
20 PRINT PAPER 5;centre 0;scale 3,3;" Lemniscate of Bernoulli "
30 LET u=scrw/2: LET v=scrh/2
40 LET n=200: LET s=1/(n-1)
50 DIM x(n): DIM y(n)
60 PLOT 0,v: DRAW scrw,0: PLOT u,(scrh-270)/2: DRAW 0,270
70 INK 2
80 LET t=0
90 FOR i=1 TO n
100 LET x(i)=SQR ABS (t+t*t): LET y(i)=SQR ABS (t-t*t)
110 LET t=t+s
120 NEXT i
130 FOR c=10 TO 280 STEP 15
140 PLOT u,v
150 FOR j=1 TO -1 STEP -2
160 FOR i=1 TO n
170 DRAW to u+j*c*x(i),v+c*y(i)
180 NEXT i
190 FOR i=n TO 1 STEP -1
200 DRAW to u+j*c*x(i),v-c*y(i)
210 NEXT i
220 NEXT j
230 NEXT c
240 PAUSE 0
