ZXASCII
AUTO -1
PROG demos:games/crapcastlemaster
CHANGED FALSE
1 REM Crap Castle Master by A Lycett adapted to SpecBAS
5 WINDOW ORIGIN 0,0,0 TO 256,192: SCALE SCRw/256,SCRh/183
10 LET XCam=4: LET YCam=-20: LET ZCam=4: LET FocLen=200
20 LET HAng=100: LET VAng=0: LET GotKey=0: GO SUB 3000
30 GO SUB 1000: GO SUB 9000: GO SUB 7000
40 PRINT AT 19,0;"(Press Any Key) ": GO SUB 200: GO SUB 5000
50 GO SUB 210
60 LET XCam=XCam+uSinH*((I$="w")-(I$="q"))+uCosH*CosV*((I$="y")-(I$="g")): IF ABS XCam<5 AND ABS YCam<3 THEN LET XCam=SGN XCam*5
70 LET YCam=YCam-uCosH*((I$="w")-(I$="q"))+uSinH*CosV*((I$="y")-(I$="g")): IF (ABS YCam)<3 AND ABS XCam<5 THEN LET YCam=SGN YCam*3
80 LET ZCam=ZCam+(I$="i")-(I$="j")+SinV*((I$="y")-(I$="g")): IF ZCam<0 THEN LET ZCam=0
90 LET HAng=HAng+5*((I$="z")-(I$="x")): IF HAng>=360 THEN LET HAng=HAng-360
100 IF HAng<0 THEN LET HAng=HAng+360
110 LET VAng=VAng+5*((I$="p")-(I$="l")): IF ABS VAng>85 THEN LET VAng=SGN VAng*85
120 GO SUB 3000
130 IF GotKey=0 AND ABS(XCam-X(Pnts-4))<=2 AND ABS(YCam-Y(Pnts-4))<=2 AND ABS(ZCam-Z(Pnts-4))<=4 THEN GO TO 160
140 IF ABS XCam<=1 AND YCam=-3 AND ABS ZCam<=5 THEN GO TO 2000+100*GotKey
150 GO SUB 7000: GO SUB 5000: GO TO 50
160 BEEP .1,0: BEEP .1,5: BEEP .1,10: BEEP .2,20: BEEP .3,30
170 PRINT PAPER 1;INK 6;AT 11,9;"Picked Up Key!"
180 LET GotKey=1: LET Pnts=Pnts-9: GO TO 150
200 LET I$=INKEY$: IF I$<>"" THEN GO TO 200
210 LET I$=INKEY$: IF I$="" THEN GO TO 210
220 RETURN
1000 REM Setup Title Screen
1010 CLS: PRINT PAPER 6;INK 2;SCALE 5,5;" CRAP CASTLE MASTER "
1040 PRINT '" Controls: ";INK 2;"Q";INK 1;" - Move Left"
1050 PRINT TAB 11;INK 2;"W";INK 1;" - Move Right"
1060 PRINT 'TAB 11;INK 2;"Z";INK 1;" - Turn Left"
1070 PRINT TAB 11;INK 2;"X";INK 1;" - Turn Right"
1080 PRINT 'TAB 11;INK 2;"Y";INK 1;" - Move Forward"
1090 PRINT TAB 11;INK 2;"G";INK 1;" - Move Backward"
1100 PRINT 'TAB 11;INK 2;"I";INK 1;" - Rise"
1110 PRINT TAB 11;INK 2;"J";INK 1;" - Fall"
1120 PRINT 'TAB 11;INK 2;"P";INK 1;" - Look Upward"
1130 PRINT TAB 11;INK 2;"L";INK 1;" - Look Downward"
1140 PRINT AT 19,0;"(Please Wait...)"
1150 PRINT PAPER 6;"                                "
1160 PRINT PAPER 6;"   (A ";INK 2;"monty.mole";INK 0;" Production)    "
1170 TEXT INK 1;PAPER 6;"    2004 A.L. Graphics Ltd.    "
1180 RETURN
2000 REM You Lose
2010 PRINT AT 10,10;PAPER 2;INK 6;"YOU LOSE !!"
2020 PRINT AT 12,5;PAPER 1;INK 6;"Better Luck Next Time!"
2030 BEEP .5,-20: GO TO 2140
2100 REM You Win
2110 PRINT AT 10,10;PAPER 2;INK 6;"WELL DONE !!"
2120 PRINT AT 12,1;PAPER 1;INK 6;"You've Unlocked The Treasure!"
2130 BEEP .5,20
2140 PRINT AT 21,0;"(Press Any Key)": GO SUB 200
2150 GO TO 10
3000 REM Calculate Focal Point
3010 LET HRad=HAng*PI/180: LET VRad=VAng*PI/180
3020 LET uSinH=SIN HRad: LET SinV=SIN VRad
3030 LET uCosH=COS HRad: LET CosV=COS VRad
3040 LET XMid=XCam+CosV*uCosH
3050 LET YMid=YCam+CosV*uSinH
3060 LET ZMid=ZCam+SinV
3070 RETURN
5000 REM Update Screen
5010 PRINT AT 0,0;: FOR I=0 TO 21: PRINT PAPER 5-(I>10+30*(ZMid-ZCam));"                                "
5020 NEXT I: GO SUB 5090
5030 FOR I=1 TO Pnts: IF C(I)=0 THEN GO TO 5080
5040 INK 2*(I>Pnts-9 AND GotKey=0)
5050 LET P=B(I): IF P>0 THEN IF C(P)=1 THEN GO SUB 6000
5060 LET P=A(I): IF P>0 THEN IF C(P)=1 THEN GO SUB 6000
5070 REM IF I(I)\#2000 AND I(I)\#199255 AND J(I)\#2000 AND J(I)\#199175 THEN PLOT I(I),J(I)
5080 NEXT I: INK 0: WAIT SCREEN
5090 IF GotKey=1 THEN PRINT PAPER 2;INK 7;AT 21,29;"KEY"
5100 RETURN
6000 REM Draw Lines Between Points
6010 LET X1=I(I): LET X2=I(P): LET Y1=J(I): LET Y2=J(P): IF X1>=0 AND X1<=255 AND Y1>=0 AND Y1<=175 AND X2>=0 AND X2<=255 AND Y2>=0 AND Y2<=175 THEN GO TO 6140
6020 IF (X1<0 AND X2<0) OR (X1>255 AND X2>255) OR (Y1<0 AND Y2<0) OR (Y1>175 AND Y2>175) THEN GO TO 6150
6030 IF X1=X2 THEN GO TO 6080
6040 IF X1<0 THEN LET Y1=Y2-(Y2-Y1)*X2/(X2-X1): LET X1=0
6050 IF X1>255 THEN LET Y1=Y2+(Y1-Y2)*(255-X2)/(X1-X2): LET X1=255
6060 IF X2<0 THEN LET Y2=Y1-(Y1-Y2)*X1/(X1-X2): LET X2=0
6070 IF X2>255 THEN LET Y2=Y1+(Y2-Y1)*(255-X1)/(X2-X1): LET X2=255
6080 IF Y1=Y2 THEN GO TO 6130
6090 IF Y1<0 THEN LET X1=X2-(X2-X1)*Y2/(Y2-Y1): LET Y1=0
6100 IF Y2<0 THEN LET X2=X1-(X1-X2)*Y1/(Y1-Y2): LET Y2=0
6110 IF Y1>175 THEN LET X1=X2+(X1-X2)*(175-Y2)/(Y1-Y2): LET Y1=175
6120 IF Y2>175 THEN LET X2=X1+(X2-X1)*(175-Y1)/(Y2-Y1): LET Y2=175
6130 IF X1<0 OR X1>255 OR Y1<0 OR Y1>175 OR X2<0 OR X2>255 OR Y2<0 OR Y2>175 THEN GO TO 6150
6140 PLOT X1,176-Y1: DRAW X2-X1,-(Y2-Y1)
6150 RETURN
7000 REM Calculate 2D Coords
7010 LET X=YCam-YMid: LET Y=ZCam-ZMid: GO SUB 7500: LET Omega=-ArcTan: LET SO=SIN -Omega: LET CO=COS -Omega
7020 LET X=XCam-XMid: LET Y=SO*(YCam-YMid)+CO*(ZCam-ZMid): GO SUB 7500: LET Phi=ArcTan
7030 LET X=-SIN -Phi*CO: LET Y=-SO: GO SUB 7500: LET Kappa=ArcTan
7040 LET SO=SIN Omega: LET SP=SIN Phi: LET SK=SIN Kappa
7050 LET CO=COS Omega: LET CP=COS Phi: LET CK=COS Kappa
7060 LET R11=CP*CK
7070 LET R12=CO*SK+SO*SP*CK
7080 LET R13=SO*SK-CO*SP*CK
7090 LET R21=-CP*SK
7100 LET R22=CO*CK-SO*SP*SK
7110 LET R23=SO*CK+CO*SP*SK
7120 LET R31=SP: LET R32=-SO*CP: LET R33=CO*CP
7130 FOR I=1 TO Pnts
7140 LET X=XCam-X(I): LET Y=YCam-Y(I): LET Z=ZCam-Z(I)
7150 LET Dist=R31*X+R32*Y+R33*Z: IF Dist<=0 THEN GO TO 7180
7160 LET I(I)=128-INT(FocLen*(R11*X+R12*Y+R13*Z)/Dist)
7170 LET J(I)=88-INT(FocLen*(R21*X+R22*Y+R23*Z)/Dist)
7180 LET C(I)=(Dist>0)
7190 NEXT I: RETURN
7500 REM ArcTan Function
7510 IF Y=0 THEN LET ArcTan=PI/2: GO TO 7540
7520 LET ArcTan=ATN ABS(X/Y)
7530 IF Y<0 THEN LET ArcTan=PI-ArcTan
7540 IF X<0 THEN LET ArcTan=-ArcTan
7550 RETURN
9000 REM Read Point Data
9010 RESTORE 9100: READ Pnts
9020 DIM X(Pnts): DIM Y(Pnts): DIM Z(Pnts)
9030 DIM A(Pnts): DIM B(Pnts): DIM C(Pnts)
9040 DIM I(Pnts): DIM J(Pnts)
9050 FOR I=1 TO Pnts
9060 READ X(I): READ Y(I): READ Z(I)
9070 READ A(I): READ B(I)
9080 NEXT I: RETURN
9100 DATA 58
9110 DATA -5,-3,0,2,5
9120 DATA 5,-3,0,3,14
9130 DATA 5,3,0,4,19
9140 DATA -5,3,0,1,28
9150 DATA -5,-3,8,6,0
9160 DATA -3,-3,8,7,0
9170 DATA -3,-3,6,8,0
9180 DATA -1,-3,6,9,0
9190 DATA -1,-3,8,10,0
9200 DATA 1,-3,8,11,0
9210 DATA 1,-3,6,12,0
9220 DATA 3,-3,6,13,0
9230 DATA 3,-3,8,14,0
9240 DATA 5,-3,8,15,0
9250 DATA 5,-1,8,16,0
9260 DATA 5,-1,6,17,0
9270 DATA 5,1,6,18,0
9280 DATA 5,1,8,19,0
9290 DATA 5,3,8,20,0
9300 DATA 3,3,8,21,0
9310 DATA 3,3,6,22,0
9320 DATA 1,3,6,23,0
9330 DATA 1,3,8,24,0
9340 DATA -1,3,8,25,0
9350 DATA -1,3,6,26,0
9360 DATA -3,3,6,27,0
9370 DATA -3,3,8,28,0
9380 DATA -5,3,8,29,0
9390 DATA -5,1,8,30,0
9400 DATA -5,1,6,31,0
9410 DATA -5,-1,6,32,0
9420 DATA -5,-1,8,5,0
9430 DATA -1,-3,0,34,0
9440 DATA -1,-3,4,35,0
9450 DATA 0,-3,5,36,0
9460 DATA 1,-3,4,37,0
9470 DATA 1,-3,0,0,0
9480 DATA 0,0,0,39,0
9490 DATA 0,0,12,40,0
9500 DATA 2,0,11,41,0
9510 DATA 0,0,10,0,0
9520 DATA -4,-3,2,43,0
9530 DATA -3,-3,2,44,0
9540 DATA -3,-3,4,45,0
9550 DATA -4,-3,4,42,0
9560 DATA 3,-3,2,47,0
9570 DATA 4,-3,2,48,0
9580 DATA 4,-3,4,49,0
9590 DATA 3,-3,4,46,0
9600 DATA -16,8,0,51,0
9610 DATA -15,8,0,54,0
9620 DATA -16,9,0,53,0
9630 DATA -15,9,0,0,0
9640 DATA -15,10,0,0,0
9650 DATA -16,10,0,56,0
9660 DATA -14,10,0,57,0
9670 DATA -14,11,0,58,0
9680 DATA -16,11,0,55,0
